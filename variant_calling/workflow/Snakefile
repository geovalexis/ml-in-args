configfile: "config/config.yaml"
samples = [os.path.splitext(entry.name)[0] for entry in os.scandir(config["samples_dir"]) if entry.is_file()]

rule all:
    input:
        "results/all.filtered.snps.extract.tsv"

rule map_reads:
    input:
        ref=config["reference_genome"],
        fasta=config["samples_dir"]+"/{sample}.fna"
    output:
        temp("results/mapped/{sample}.bam")
    conda:
        "envs/mapping.yaml"
    shell:
        "bwa mem -t {threads} {input.ref} {input.fasta} | samtools view -Sb - > {output}"

rule sort_alignments:
    input:
        "results/mapped/{sample}.bam"
    output:
        temp("results/mapped/{sample}.sorted.bam")
    conda:
        "envs/mapping.yaml"
    shell:
        "samtools sort -o {output} {input}"

rule call_variants:
    input:
        ref=config["reference_genome"],
        bam=expand("results/mapped/{sample}.sorted.bam", sample=samples)
    output:
        temp("results/calls/all.vcf.gz")
    conda:
        "envs/calling.yaml"
    shell:
        "bcftools mpileup -f {input.ref} {input.bam} | bcftools call -mv -Oz > {output}"

rule filter_variants:
    input:
        "results/calls/all.vcf.gz"
    output:
        temp("results/filters/all.filtered.vcf")
    conda:
        "envs/filtering.yaml"
    shell:
        "vcftools --gzvcf {input} --minDP 4 --max-missing 0.2 --minQ 30 --recode --recode-INFO-all --stdout > {output}"

rule keep_only_snps:
    input:
        "results/filters/all.filtered.vcf"
    output:
        temp("results/filters/all.filtered.snps.vcf")
    conda:
        "envs/filtering.yaml"
    shell:
        "vcftools --vcf {input} --remove-indels --recode --recode-INFO-all --stdout > {output}"

rule select_columns:
    input:
        "results/filters/all.filtered.snps.vcf"
    output:
        "results/all.filtered.snps.extract.tsv"
    conda:
        "envs/calling.yaml"
    shell:
        "bcftools query -f '%CHROM\t%POS\t%REF\t%ALT\t[%TGT]\n' {input} -o {output}"